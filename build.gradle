// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.31'
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.21.0"
    }
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    repositories {
        google()
        maven { url "${artifactory_contextUrl}/gligar-gradle-dev-local" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    packageName = "com.opensooq.supernova"
    artifactId = "gligar"
    libraryVersion = "1.1.1"

    deploymentEnvironment = 'dev'

    repoKey = "gligar-gradle-${deploymentEnvironment}-local"
    resolverRepoKey = "gligar-gradle-${deploymentEnvironment}"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId rootProject.ext.packageName
            version = rootProject.ext.libraryVersion
            artifactId rootProject.ext.artifactId
            artifact("$projectDir/gligar/build/outputs/aar/gligar-release.aar")
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = rootProject.ext.repoKey
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = rootProject.ext.resolverRepoKey
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
}
